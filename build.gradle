plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'application'
}

group = 'jp.juggler'
version = '1.0-SNAPSHOT'

repositories {
    maven { url "https://kotlin.bintray.com/kotlinx" }
    mavenCentral()
    jcenter()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    implementation "org.jetbrains.kotlin:kotlin-util-io:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-cli:0.3"


    def ktor_version="1.4.3"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"

    implementation "org.apache.commons:commons-text:1.9"
}

test {
    useJUnit()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalStdlibApi"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

def nameMainClass = 'MainKt'

application {
    mainClassName = nameMainClass
}

task fatJar(type: Jar) {
    archiveBaseName.set( "feedToMastodon" )
    // sources とかjavadocとか。実行バイナリだとセットしない？
    // archiveClassifier.set('all')

    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
     //   configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }{
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes "Manifest-Version": "1.0", "Main-Class": nameMainClass
    }
}